package rulesBackward;

import com.ftn.sbnz.model.AugmentLocation;
import com.ftn.sbnz.model.Augment;
import com.ftn.sbnz.model.Result;

query isAncestor( String x, String y )
    AugmentLocation( x, y; )
or
    (AugmentLocation( z, y; ) and isAncestor( x, z; ) )
end

query areSiblings(String x, String y)
    AugmentLocation(x, z;) and AugmentLocation(y, z;)
end

rule "ancestor"
activation-group "areConnectedGroup"
when
    $aug1: Augment()
    $aug2: Augment(name != $aug1.name)
    isAncestor($aug1.name, $aug2.name; )
    $result: Result()
then
    modify($result) {
        setValue("They are connected, parent is=" + $aug2.getName() + ", child name is=" + $aug1.getName());
    }
end

rule "siblings"
activation-group "areConnectedGroup"
when
    $aug1: Augment()
    $aug2: Augment(name != $aug1.name)
    areSiblings($aug1.name, $aug2.name; )
    $result: Result()
then
    modify($result) {
        setValue("They are connected, sibling one is=" + $aug2.getName() + ", sibling two is=" + $aug1.getName());
    }
end
